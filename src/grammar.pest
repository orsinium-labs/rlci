module = { SOI ~ WSPACE* ~ statement+ ~ EOI }
statement = { ( assignment | expression) ~ " "* ~ (EOI | NEWLINE+) }
  assignment = { identifier ~ " "* ~ "=" ~ " "* ~ expression }
  expression = { definition | call | identifier | ( "(" ~ expression ~ ")" ) }
    call = {
        ( identifier | ( "(" ~ expression ~ ")" ) )
        ~ (
            " "+
            ~ ( identifier | ( "(" ~ expression ~ ")" ) )
        )+
    }
    definition = { ("\\" | "Î»") ~ identifier ~ " "+ ~ expression }
    identifier = { (ASCII_ALPHANUMERIC | "_")+ | PUNCT+ }

WSPACE = _{ " " | "\t" | "\u{000C}" | "\r" | "\n" }
PUNCT = {
    // there is no `=` yet because it might be ambiguous when assigning to it
    "!" | "#" | "$" | "%" | "&" | "*" | "+" | "-" |
    "/" | "<" | ">" | "?" | "@" | "^" | "~" | "|" |
    // allow unicode math symbols
    // https://en.wikipedia.org/wiki/Mathematical_operators_and_symbols_in_Unicode
    '\u{2200}'..'\u{22FF}'
}
